plugins {
    id 'java'
}

group = 'nekonic'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()// Maven 중앙 저장소
    maven { url = 'https://papermc.io/repo/repository/maven-public/' }
}


dependencies {
    // JUnit 5 (Jupiter) - 테스트용
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.5'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.5'

    // Mockito - 테스트용
    testImplementation 'org.mockito:mockito-core:5.13.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.13.0'

    // AssertJ - 테스트용
    testImplementation 'org.assertj:assertj-core:3.25.3'

    // Paper API - 서버 빌드 시 필요
    compileOnly 'io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT'

    // SQLite - JDBC 드라이버
    implementation 'org.xerial:sqlite-jdbc:3.41.2.2'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(22))
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

jar {
    archiveFileName = 'Mineconomy.jar'
    if (!System.getenv("CI")) { // GitHub Actions 환경이 아닐 경우에만 적용
        destinationDirectory = file("D:\\1\\java\\server\\plugins")
    }
}

tasks.withType(JavaCompile){
    options.encoding = "UTF-8"
}

test {
    useJUnitPlatform() // JUnit 5 플랫폼에서 테스트 실행
}